<!DOCTYPE html>
<html>

<head>
  <meta charset="UTF-8">
  
  <script type="text/javascript" src="http://code.jquery.com/jquery-1.9.1.min.js"></script>
  <script type="text/javascript" src="gl-matrix.js"></script>
  <script type="text/javascript" src="webgl-utils.js"></script>
  <script type="text/javascript" src="volume-meter.js"></script>

  <!-- Main Shader -->
  
  <script id="shader-fs" type="x-shader/x-fragment">
    precision mediump float;

    varying vec2 vTextureCoord;
    varying vec3 vTransformedNormal;
    varying vec4 vPosition;

    uniform vec3 uAmbientColor;

    uniform vec4 uColor;
    uniform vec3 uLightLocation;
    uniform vec3 uSpotDirection;
    uniform vec3 uLightSpecularColor;
    uniform vec3 uLightDiffuseColor;
    uniform bool uHasTexure;

    uniform sampler2D uSampler;

    uniform float uMaterialShininess;

    const float spotExponent = 40.0;
    const float spotCosCutoff = 0.9;   // corresponds to 14 degrees

    vec3 lightWeighting = vec3( 0.0, 0.0, 0.0 );
    vec4 fragmentColor;
    float spotEffect, rdotv, specularLightWeighting, diffuseLightWeighting;

    void main(void) {
      const float LOG2 = 1.442695;
      float z = gl_FragCoord.z / gl_FragCoord.w;
      
      vec3 vectorToLightSource = normalize(uLightLocation - vPosition.xyz / vPosition.w);

      diffuseLightWeighting = max(dot(vTransformedNormal, vectorToLightSource), 0.0);

      if (diffuseLightWeighting > 0.0) {
        spotEffect = dot(normalize(uSpotDirection), normalize(-vectorToLightSource));

        if (spotEffect > spotCosCutoff) {
          spotEffect = pow(spotEffect, spotExponent);

          vec3 reflectionVector = normalize(reflect(-vectorToLightSource, vTransformedNormal));
  
          vec3 viewVectorEye = -normalize(vPosition.xyz / vPosition.w);
          rdotv = max(dot(reflectionVector, viewVectorEye), 0.0);
          specularLightWeighting = pow(rdotv, uMaterialShininess);

          lightWeighting =
            spotEffect * uLightDiffuseColor * diffuseLightWeighting +
            spotEffect * uLightSpecularColor * specularLightWeighting;
        }
      }

      
      lightWeighting += uAmbientColor;
      if(uHasTexure){
        vec4 fragmentColor = texture2D(uSampler, vec2(vTextureCoord.s, vTextureCoord.t));
        gl_FragColor = vec4(fragmentColor.rgb * lightWeighting, fragmentColor.a);
        gl_FragColor = mix(gl_FragColor, vec4(uColor.rgb * lightWeighting, uColor.a), 0.5);
      }
      else{
        gl_FragColor = vec4(uColor.rgb * lightWeighting, uColor.a);
      }
    }
  </script>

  <script id="shader-vs" type="x-shader/x-vertex">
    attribute vec3 aVertexPosition;
    attribute vec3 aVertexNormal;
    attribute vec2 aTextureCoord;

    uniform mat4 uMVMatrix;
    uniform mat4 uPMatrix;
    uniform mat3 uNMatrix;

    varying vec2 vTextureCoord;
    varying vec3 vTransformedNormal;
    varying vec4 vPosition;
    varying vec4 vColor;


    void main(void) {
        vPosition = uMVMatrix * vec4(aVertexPosition, 1.0);
        gl_Position = uPMatrix * vPosition;
        vTextureCoord = aTextureCoord;
        vTransformedNormal = uNMatrix * aVertexNormal;
    }
  </script>

  <script id="SpectrumShader-vs" type="x-shader/x-vertex">
    
    attribute vec3 aPos;
     
    void main()
    {
        gl_Position = vec4(aPos.x, aPos.y, aPos.z, 1.0);
        gl_PointSize = 3.0;
    }
  </script>

  <script id="SpectrumShader-fs" type="x-shader/x-fragment">
    precision mediump float;
    //out vec4 FragColor;
    
    uniform vec4 ourColor;
    
    float u_hue=121.0;//uniform
    float u_saturation=2.0;//uniform 
    float u_value=1.0;//uniform 
    float u_contrast=1.0;//uniform 
    
    vec3 rgbtohsv(vec3 rgb)
    {
        float R = rgb.x;
        float G = rgb.y;
        float B = rgb.z;
        vec3 hsv;
        float max1 = max(R, max(G, B));
        float min1 = min(R, min(G, B));
        if (R == max1)
        {
            hsv.x = (G - B) / (max1 - min1);
        }
        if (G == max1)
        {
            hsv.x = 2.0 + (B - R) / (max1 - min1);
        }
        if (B == max1)
        {
            hsv.x = 4.0 + (R - G) / (max1 - min1);
        }
        hsv.x = hsv.x * 60.0;
        if (hsv.x < 0.0)
        {
            hsv.x = hsv.x + 360.0;
        }
        hsv.z = max1;
        hsv.y = (max1 - min1) / max1;
        return hsv;
    }
    vec3 hsvtorgb(vec3 hsv)
    {
        float R;
        float G;
        float B;
        if (hsv.y == 0.0)
        {
            R = G = B = hsv.z;
        }
        else
        {
            hsv.x = hsv.x / 60.0;
            int i = int(hsv.x);
            float f = hsv.x - float(i);
            float a = hsv.z * (1.0 - hsv.y);
            float b = hsv.z * (1.0 - hsv.y * f);
            float c = hsv.z * (1.0 - hsv.y * (1.0 - f));
            if (i == 0)
            {
                R = hsv.z;
                G = c;
                B = a;
            }
            else if (i == 1)
            {
                R = b;
                G = hsv.z;
                B = a;
            }
            else if (i == 2)
            {
                R = a;
                G = hsv.z;
                B = c;
            }
            else if (i == 3)
            {
                R = a;
                G = b;
                B = hsv.z;
            }
            else if (i == 4)
            {
                R = c;
                G = a;
                B = hsv.z;
            }
            else
            {
                R = hsv.z;
                G = a;
                B = b;
            }
        }
        return vec3(R, G, B);
    }
    void main()
    {
        
        vec4 pixColor = ourColor;
        vec3 hsv;
        hsv.xyz = rgbtohsv(pixColor.rgb);
        hsv.x += u_hue;
        hsv.x = mod(hsv.x, 360.0);
        hsv.y *= u_saturation;
        hsv.z *= u_value;
        vec3 f_color = hsvtorgb(hsv);
        f_color = ((f_color - 0.5) * max(u_contrast+1.0, 0.0)) + 0.5;
        
        gl_FragColor = vec4(f_color, pixColor.a);
        
        //FragColor = ourColor*vec4(1.0,1.0,1.0,0.5);
    } 
  </script>

  <script type='text/javascript' src='webgl-obj-loader.js'></script>
  <script type='text/javascript' src='globals.js'></script>
  <script type='text/javascript' src='helpers.js'></script>
  <script type='text/javascript' src='initialize.js'></script>
  <script type='text/javascript' src='camera.js'></script>
  <script type='text/javascript' src='scene.js'></script>
  <script type='text/javascript' src='ColorUtil.js'></script>
  <script type='text/javascript' src='webgl.js'></script>
</head>

<body style='padding:0; margin:0 auto;'>

  <div style='width:50%; '>
    <canvas id="mycanvas" style="margin:0 auto;" width="1080" height="675"></canvas><br />
    <canvas id="canvas" width="1080" height="500"></canvas> 
  </div>
</body>

</html>